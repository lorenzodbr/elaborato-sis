.model datapath
.inputs INOUT1 INOUT0 SECTOR2 SECTOR1 SECTOR0 STOREA STOREB STOREC IS_REQ_VALID
.outputs SECTOR_A SECTOR_B SECTOR_C SBARRA

########
#SEARCH#
########

.search const24.blif
.search const31.blif
.search const0.blif
.search const1.blif
.search encoder3to2.blif
.search uguale5.blif
.search minimo5.blif
.search massimo5.blif
.search registro5.blif
.search mux2i1b.blif
.search mux4i5b.blif
.search fulladder5.blif
.search sottrattore5.blif
.search is_valid_for.blif
.search not.blif

##########
#COSTANTI#
##########

.subckt const24 O4=O4_24 O3=O3_24 O2=O2_24 O1=O1_24 O0=O0_24
.subckt const31 O4=O4_31 O3=O3_31 O2=O2_31 O1=O1_31 O0=O0_31
.subckt const1 O=O_1
.subckt const0 O=O_0

###################################
#MEMORIZZAZIONE VALORI DEI SETTORI#
###################################

#Selettore del minimo per impostare il valore nei registri come da specifica (solo per il settore C)
.subckt minimo5 A4=O4_24 A3=O3_24 A2=O2_24 A1=O1_24 A0=O0_24 B4=INOUT1 B3=INOUT0 B2=SECTOR2 B1=SECTOR1 B0=SECTOR0 O4=SECTOR_C_MIN_4 O3=SECTOR_C_MIN_3 O2=SECTOR_C_MIN_2 O1=SECTOR_C_MIN_1 O0=SECTOR_C_MIN_0

.subckt mux2i5b S=STOREA A4=TO_REG_A_4 A3=TO_REG_A_3 A2=TO_REG_A_2 A1=TO_REG_A_1 A0=TO_REG_A_0 B4=INOUT1 B3=INOUT0 B2=SECTOR2 B1=SECTOR1 B0=SECTOR0 O4=PREV_OR_NEW_SECTOR_A_4 O3=PREV_OR_NEW_SECTOR_A_3 O2=PREV_OR_NEW_SECTOR_A_2 O1=PREV_OR_NEW_SECTOR_A_1 O0=PREV_OR_NEW_SECTOR_A_0
.subckt mux2i5b S=STOREB A4=TO_REG_B_4 A3=TO_REG_B_3 A2=TO_REG_B_2 A1=TO_REG_B_1 A0=TO_REG_B_0 B4=INOUT1 B3=INOUT0 B2=SECTOR2 B1=SECTOR1 B0=SECTOR0 O4=PREV_OR_NEW_SECTOR_B_4 O3=PREV_OR_NEW_SECTOR_B_3 O2=PREV_OR_NEW_SECTOR_B_2 O1=PREV_OR_NEW_SECTOR_B_1 O0=PREV_OR_NEW_SECTOR_B_0
.subckt mux2i5b S=STOREC A4=TO_REG_C_4 A3=TO_REG_C_3 A2=TO_REG_C_2 A1=TO_REG_C_1 A0=TO_REG_C_0 B4=SECTOR_C_MIN_4 B3=SECTOR_C_MIN_3 B2=SECTOR_C_MIN_2 B1=SECTOR_C_MIN_1 B0=SECTOR_C_MIN_0 O4=PREV_OR_NEW_SECTOR_C_4 O3=PREV_OR_NEW_SECTOR_C_3 O2=PREV_OR_NEW_SECTOR_C_2 O1=PREV_OR_NEW_SECTOR_C_1 O0=PREV_OR_NEW_SECTOR_C_0

#Registri per memorizzare i valori dei settori
.subckt registro5 A4=PREV_OR_NEW_SECTOR_A_4 A3=PREV_OR_NEW_SECTOR_A_3 A2=PREV_OR_NEW_SECTOR_A_2 A1=PREV_OR_NEW_SECTOR_A_1 A0=PREV_OR_NEW_SECTOR_A_0 O4=REG_A_4 O3=REG_A_3 O2=REG_A_2 O1=REG_A_1 O0=REG_A_0
.subckt registro5 A4=PREV_OR_NEW_SECTOR_B_4 A3=PREV_OR_NEW_SECTOR_B_3 A2=PREV_OR_NEW_SECTOR_B_2 A1=PREV_OR_NEW_SECTOR_B_1 A0=PREV_OR_NEW_SECTOR_B_0 O4=REG_B_4 O3=REG_B_3 O2=REG_B_2 O1=REG_B_1 O0=REG_B_0
.subckt registro5 A4=PREV_OR_NEW_SECTOR_C_4 A3=PREV_OR_NEW_SECTOR_C_3 A2=PREV_OR_NEW_SECTOR_C_2 A1=PREV_OR_NEW_SECTOR_C_1 A0=PREV_OR_NEW_SECTOR_C_0 O4=REG_C_4 O3=REG_C_3 O2=REG_C_2 O1=REG_C_1 O0=REG_C_0

#Uscite per indicare l'occupazione completa o meno dei settori
.subckt uguale5 A4=TO_REG_A_4 A3=TO_REG_A_3 A2=TO_REG_A_2 A1=TO_REG_A_1 A0=TO_REG_A_0 B4=O4_31 B3=O3_31 B2=O2_31 B1=O1_31 B0=O0_31 O=SECTOR_A
.subckt uguale5 A4=TO_REG_B_4 A3=TO_REG_B_3 A2=TO_REG_B_2 A1=TO_REG_B_1 A0=TO_REG_B_0 B4=O4_31 B3=O3_31 B2=O2_31 B1=O1_31 B0=O0_31 O=SECTOR_B
.subckt uguale5 A4=TO_REG_C_4 A3=TO_REG_C_3 A2=TO_REG_C_2 A1=TO_REG_C_1 A0=TO_REG_C_0 B4=O4_24 B3=O3_24 B2=O2_24 B1=O1_24 B0=O0_24 O=SECTOR_C

#Istanziamento dell'encoder dei settori
.subckt encoder3to2 I2=SECTOR2 I1=SECTOR1 I0=SECTOR0 O1=SECTOR_ENCODED_1 O0=SECTOR_ENCODED_0

#Indirizzamento dei valori dei registri per l'elaborazione
.subckt mux4i5b S1=SECTOR_ENCODED_1 S0=SECTOR_ENCODED_0 A4=O_0 A3=O_0 A2=O_0 A1=O_0 A0=O_0 B4=REG_C_4 B3=REG_C_3 B2=REG_C_2 B1=REG_C_1 B0=REG_C_0 C4=REG_B_4 C3=REG_B_3 C2=REG_B_2 C1=REG_B_1 C0=REG_B_0 D4=REG_A_4 D3=REG_A_3 D2=REG_A_2 D1=REG_A_1 D0=REG_A_0 O4=TO_OPERATION_4 O3=TO_OPERATION_3 O2=TO_OPERATION_2 O1=TO_OPERATION_1 O0=TO_OPERATION_0

###################
#ELABORAZIONE DATI#
###################

##################
#INCREMENTO POSTI#
##################

#Prelevamento delle costanti di posti massimi
.subckt mux4i5b S1=SECTOR_ENCODED_1 S0=SECTOR_ENCODED_0 A4=O_0 A3=O_0 A2=O_0 A1=O_0 A0=O_0 B4=O4_24 B3=O3_24 B2=O2_24 B1=O1_24 B0=O0_24 C4=O4_31 C3=O3_31 C2=O2_31 C1=O1_31 C0=O0_31 D4=O4_31 D3=O3_31 D2=O2_31 D1=O1_31 D0=O0_31 O4=MAX_PER_SECTOR_4 O3=MAX_PER_SECTOR_3 O2=MAX_PER_SECTOR_2 O1=MAX_PER_SECTOR_1 O0=MAX_PER_SECTOR_0

#Confronto posti attuali con posti massimi
.subckt uguale5 A4=MAX_PER_SECTOR_4 A3=MAX_PER_SECTOR_3 A2=MAX_PER_SECTOR_2 A1=MAX_PER_SECTOR_1 A0=MAX_PER_SECTOR_0 B4=TO_OPERATION_4 B3=TO_OPERATION_3 B2=TO_OPERATION_2 B1=TO_OPERATION_1 B0=TO_OPERATION_0 O=MAX_REACHED
.subckt mux2i5b S=MAX_REACHED A4=O_0 A3=O_0 A2=O_0 A1=O_0 A0=O_1 B4=O_0 B3=O_0 B2=O_0 B1=O_0 B0=O_0 O4=TO_INCR4 O3=TO_INCR3 O2=TO_INCR2 O1=TO_INCR1 O0=TO_INCR0

#Incremento i posti occupati
.subckt fulladder5 A4=TO_INCR4 A3=TO_INCR3 A2=TO_INCR2 A1=TO_INCR1 A0=TO_INCR0 B4=TO_OPERATION_4 B3=TO_OPERATION_3 B2=TO_OPERATION_2 B1=TO_OPERATION_1 B0=TO_OPERATION_0 CIN=O_0 O4=INCR4 O3=INCR3 O2=INCR2 O1=INCR1 O0=INCR0

##################
#DECREMENTO POSTI#
##################

#Per evitare overflow negativo, scelgo il massimo tra 1 e i posti occupati
.subckt massimo5 A4=TO_OPERATION_4 A3=TO_OPERATION_3 A2=TO_OPERATION_2 A1=TO_OPERATION_1 A0=TO_OPERATION_0 B4=O_0 B3=O_0 B2=O_0 B1=O_0 B0=O_1 O4=MAX4 O3=MAX3 O2=MAX2 O1=MAX1 O0=MAX0

#Decremento i posti occupati
.subckt sottrattore5 A4=MAX4 A3=MAX3 A2=MAX2 A1=MAX1 A0=MAX0 B4=O_0 B3=O_0 B2=O_0 B1=O_0 B0=O_1 O4=DECR4 O3=DECR3 O2=DECR2 O1=DECR1 O0=DECR0

#Smisto le operazioni effettuate
.subckt mux4i5b S1=INOUT1 S0=INOUT0 A4=O_0 A3=O_0 A2=O_0 A1=O_0 A0=O_0 B4=INCR4 B3=INCR3 B2=INCR2 B1=INCR1 B0=INCR0 C4=DECR4 C3=DECR3 C2=DECR2 C1=DECR1 C0=DECR0 D4=O_0 D3=O_0 D2=O_0 D1=O_0 D0=O_0 O4=OPERATION4 O3=OPERATION3 O2=OPERATION2 O1=OPERATION1 O0=OPERATION0

#Controllo che il risultato dell'elaborazione sia destinato ad un determinato registro, altrimenti riporto il valore precedente
.subckt is_valid_for SECTOR_ENCODED_1=SECTOR_ENCODED_1 SECTOR_ENCODED_0=SECTOR_ENCODED_0 IS_REQ_VALID=IS_REQ_VALID STOREA=STOREA STOREB=STOREB STOREC=STOREC IS_VALID_FOR_A=IS_VALID_FOR_A IS_VALID_FOR_B=IS_VALID_FOR_B IS_VALID_FOR_C=IS_VALID_FOR_C

.subckt mux2i5b S=IS_VALID_FOR_A A4=REG_A_4 A3=REG_A_3 A2=REG_A_2 A1=REG_A_1 A0=REG_A_0 B4=OPERATION4 B3=OPERATION3 B2=OPERATION2 B1=OPERATION1 B0=OPERATION0 O4=TO_REG_A_4 O3=TO_REG_A_3 O2=TO_REG_A_2 O1=TO_REG_A_1 O0=TO_REG_A_0
.subckt mux2i5b S=IS_VALID_FOR_B A4=REG_B_4 A3=REG_B_3 A2=REG_B_2 A1=REG_B_1 A0=REG_B_0 B4=OPERATION4 B3=OPERATION3 B2=OPERATION2 B1=OPERATION1 B0=OPERATION0 O4=TO_REG_B_4 O3=TO_REG_B_3 O2=TO_REG_B_2 O1=TO_REG_B_1 O0=TO_REG_B_0
.subckt mux2i5b S=IS_VALID_FOR_C A4=REG_C_4 A3=REG_C_3 A2=REG_C_2 A1=REG_C_1 A0=REG_C_0 B4=OPERATION4 B3=OPERATION3 B2=OPERATION2 B1=OPERATION1 B0=OPERATION0 O4=TO_REG_C_4 O3=TO_REG_C_3 O2=TO_REG_C_2 O1=TO_REG_C_1 O0=TO_REG_C_0

##########################
#GESTIONE APERTURA SBARRA#
##########################

#Determino lo stato della sbarra
.subckt not A=MAX_REACHED O=INV_MAX_REACHED
.subckt mux2i1b S=IS_REQ_VALID I0=INV_MAX_REACHED I1=O_0 O=TO_BE_VALIDATED_SBARRA
.subckt mux4i1b S1=INOUT1 S0=INOUT0 I3=O_0 I2=TO_BE_VALIDATED_SBARRA I1=IS_REQ_VALID I0=O_0 O=SBARRA

.end
